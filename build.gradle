plugins {
    id 'java-library'
    id 'maven-publish' // To publish into github
    id 'signing' // Required by maven central
    id 'org.sonatype.gradle.plugins.scan' version '2.8.3'
}

group 'io.github.kamilszewc'
version '1.6'

java {
    sourceCompatibility = JavaVersion.VERSION_1_9
    targetCompatibility = JavaVersion.VERSION_1_9
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                if (project.properties.containsKey('mavenCentralTokenUser')) {
                    username = "$mavenCentralTokenUser"
                }
                if (project.properties.containsKey('mavenCentralTokenPassword')) {
                    password = "$mavenCentralTokenPassword"
                }
            }
        }
    }
    publications {

        mavenJava(MavenPublication) {
            artifactId = 'java-ansi-text-colorizer'
            from components.java
            pom {
                name = 'Java ANSI text colorizer'
                description = 'Simple tool to print colored text to terminal/console'
                url = 'https://github.com/kamilszewc/java-ansi-text-colorizer'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kszewc'
                        name = "Kamil Szewc"
                        email = 'kamil.szewc@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/kamilszewc/java-ansi-text-colorizer.git'
                    developerConnection = 'scm:git:https://github.com/kamilszewc/java-ansi-text-colorizer.git'
                    url = 'https://github.com/kamilszewc/java-ansi-text-colorizer.git'
                }
            }
        }
    }
}


signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
    sign configurations.archives
}

ossIndexAudit {
    allConfigurations = false
    useCache = false
    outputFormat = 'DEFAULT'
}